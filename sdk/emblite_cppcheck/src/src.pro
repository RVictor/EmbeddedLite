TEMPLATE = app
TARGET = cppcheck
DEPENDPATH += .
INCLUDEPATH += .
OBJECTS_DIR = temp
CONFIG += warn_on
CONFIG -= qt app_bundle

include($$PWD/src.pri)
SOURCES += main.cpp

win32 {
    CONFIG += embed_manifest_exe console
    RC_FILE = cppcheck.rc
    HEADERS += resource.h
    LIBS += -lshlwapi
}

# Add more strict compiling flags for GCC
# These flags are used in original (not generated by QMake) makefiles
contains(QMAKE_CXX, g++) {
    QMAKE_CXXFLAGS_WARN_ON += -Wextra -pedantic
}

# Change Visual Studio compiler (CL) warning level to W4
contains(QMAKE_CXX, cl) {
    QMAKE_CXXFLAGS_WARN_ON -= -W3
    QMAKE_CXXFLAGS_WARN_ON += -W4
}
